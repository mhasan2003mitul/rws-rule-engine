package nl.rws.re.rules.housebuilding;
 
import nl.rws.re.facts.housebuilding.HouseBuildingService;
import nl.rws.re.facts.housebuilding.ServiceReply;
import nl.rws.re.facts.details.ApplicantDetail;
import nl.rws.re.facts.details.Information;
import nl.rws.re.facts.details.Address;

 /**
  * @author Mainul
  *
  */

rule "House Building Approve Rule" dialect "mvel" no-loop true
when
    not (HouseBuildingService())
    not(ServiceReply())
then
    insert(new ServiceReply("House building service request should be provided."));
end

rule "Validate all required field are given" dialect "mvel"
when
    HouseBuildingService()
then
    drools.setFocus("check-required-field");
end

rule "Is applicant details provided in house building request?" dialect "mvel" agenda-group "check-required-field"
when
    $houserBuildingService:HouseBuildingService(applicantDetail == null)
then
    insert(new ServiceReply("Applicant details should be provided in house building service request."));
    retract($houserBuildingService);
end

rule "Is applicant information provided in applicant detail?" dialect "mvel" agenda-group "check-required-field"
when
    $houserBuildingService:HouseBuildingService($applicantDetail:applicantDetail)
    ApplicantDetail(information == null) from $applicantDetail
then
    insert(new ServiceReply("Applicant information should be provided in applicant detail."));
    retract($houserBuildingService);
end

rule "Is applicant address provided in applicant information?" dialect "mvel" agenda-group "check-required-field"
when
    $houserBuildingService:HouseBuildingService($applicantDetail:applicantDetail)
    ApplicantDetail($information:information ) from $applicantDetail
    Information(address == null) from $information
then
    insert(new ServiceReply("Applicant address should be provided in application information."));
    retract($houserBuildingService);
end

rule "Does applicant address contain all values?" dialect "mvel" agenda-group "check-required-field"
when
    $houserBuildingService:HouseBuildingService($applicantDetail:applicantDetail)
    ApplicantDetail($information:information ) from $applicantDetail
    Information($address:address) from $information
    Address(streetName == null || houseNumber == null || postCode == null || city == null ) from $address
then
    insert(new ServiceReply("Stree Name, House Number, Post Code and City should be provided in address."));
    retract($houserBuildingService);
end


